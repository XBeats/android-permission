apply plugin: 'maven'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 开发者信息
def developer_url = 'https://github.com/XBeats'
def developer_id = 'xbeats'
def developer_name = 'xbeats'
def developer_email = 'fenghaifeng008@gmail.com'

group = bintray_group // Maven Group ID for the artifact，一般填你唯一的包名
version = bintray_version // This is the library version used when deploying the artifact
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name bintray_desc //项目描述
                url bintray_siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id developer_id    //填写的一些基本信息
                        name developer_name
                        email developer_email
                    }
                }
                scm {
                    connection bintray_gitUrl
                    developerConnection developer_url
                    url bintray_siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    options.encoding = "UTF-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = bintray_name    //发布到JCenter上的项目名字
        websiteUrl = bintray_siteUrl
        vcsUrl = bintray_gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}